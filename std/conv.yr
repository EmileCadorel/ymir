import std.traits;

def to (T of string) (c : char) : string {
    return cast:string ([c]);
}

def to (T of string, c : char) () {
    return '' + c;
}

def to (T of string)  (a : ptr!char) : string {
    if (a is null) return '';
    else if (*a == 0ub) return ''; 
    let ret = [*a];
    a = a + 1;
    while (*a != 0ub) {
	ret += [*a];
	a = a + 1;
    }
    return cast:string (ret);
}

def to (T of string) (elem : [char]) : string {
    return cast:string (elem);
}

def to if ((is (T2 : long) || is (T2 : int)))
    (T of string, T2) (elem : T2) : string {
	import std.traits;
	if (elem < 0) return "-" + to!(T) (-elem);
	else if (elem == 0) return "0";
	let nb = elem, size = 0UL;
	while (nb > 0) { nb /= to!(T2) (10B); size ++; }       
	let res = [char ; size];	
	for (it in 0UL .. size) {
	    res [size - it - 1UL] = cast:char ((elem % to!(T2) (10UB))) + '0';
	    elem /= to!(T2) (10UB);
	}	
	return cast:string (res);
    }

def to if (is (T2 : short))
    (T of string, T2) (elem : T2) : string {
	import std.traits;
	return to!(string) (cast:int(elem));
    }

def to if (is (T2 : byte))
    (T of string, T2) (elem : T2) : string {
	let aux = cast:int (elem);
	let right = aux % 16;
	let left = aux / 16;
	let ret = [char; 2UL];
	
	if (right <= 9) ret [1] = cast:char (right) + '0';
	else ret [1] = cast:char (right - 10) + 'a';

	if (left <= 9) ret [0] = cast:char (left) + '0';
	else ret [0] = cast:char (left - 10) + 'a';
	return cast:string (ret);
    }

def to if (isDecimal!(T)())
    (T) (elem : string) : T {
	import std.conv;
	let res = T.init;
	for (it in 0UL .. elem.length) {
	    let current = elem [it];	    
	    if (current >= '0' && current <= '9') {
		res *= cast:T(10UB);
		res += cast:T (current - '0');
	    } else assert (false, to!(string) (current));
	}
	return res;
    }

def to if (isUnsigned!(T2)())
    (T of string, T2) (elem : T2) : string {
	return to!(string) (toSigned (elem));
    }

def toSigned if (isUnsigned!(T)()) 
    (T) (elem : T) {
	static if (is (T : ulong)) return cast:long (elem);
	else if (is (T : uint)) return cast:int (elem);
	else if (is (T : ushort)) return cast:short (elem);
	else return cast:byte (elem);	
    }

def to if (isDecimal!(T)() && isDecimal!(T2) ())
    (T, T2) (elem : T2) {
	static if (is (T : T2)) return elem;
	else return cast:T (elem);
    }

def to if (!is (T2 : char))
    (T of string, T2) (elem : [T2]) {
	if (elem is null) return 'null';
	else {
	    let ret = "[";
	    for (it_ in 0UL .. elem.length) {
		ret += to!(string) (elem [it_]);
		if (it_ != elem.length - 1UL)
		    ret += ", ";
	    }	
	    ret += "]";
	    return ret;
	}
    }

def to (T of string) (elem : T) {
    if (elem is null) return 'null';
    else return elem;
}

def addTupleComa if (!isTuple!(T)())
    (T) (left : T) : string {
	return to!(string) (left);
    }

def addTupleComa (T) (left : T, elem) : string {
    static if (!isPrimitive!(typeof (elem))() && !isTuple!(typeof (elem))()) {
	if (elem !is null)
	    return to!(string)(left) + ", " + addTupleComa (expand (elem));
	else
	    return to!(string)(left) + ", null";
    } else {
	return to!(string) (left) + ", " + addTupleComa (expand (elem));
    }
}

def to if (isTuple!(T2) ())
    (T of string, T2) (elem : T2) : string {	
	return 'tuple(' + addTupleComa (expand (elem)) + ')';	
    }

def to if (is (T2 : struct))
    (T of string, T2) (elem : T2) {
	if (elem is null) return T2.typename;	
	else return T2.typename + "(" + addTupleComa (expand (elem.tupleof)) + ")";
    }

def to if (is (T : [ubyte]))
    (T) (a : string) {
	let res = [ubyte; a.length];
	for (it in 0UL .. a.length) {
	    res [it] = cast:ubyte (a[it]);
	}
	return res;
    }
       
def to (T of string) (elem : float) : string {
    import std.traits;
    if (elem < 0.) return "-" + to!(string) (-elem);
    else if (elem == 0.) return "0";
    let prec = 5UL;
    let f = cast:int (elem * 100000.);
    let size = 0UL, nb = f;	
    while (nb > 0) { nb /= 10; size ++; }
    let aux = [char; size + 1UL];
    for (id in 0UL .. size + 1UL) {
	if (id == prec) {aux [aux.length - id - 1UL] = '.';}
	else {
	    aux [aux.length - id - 1UL] =  cast:char (f % 10) + '0';
	    f /= 10;
	}	    
    }
    return cast:string (aux);
}
       
def to (T of string) (elem : float, prec : ulong) : string {
    import std.traits;
    if (elem < 0.) return "-" + to!(string) (-elem);
    else if (elem == 0.) return "0";
    let f = cast:ulong (elem * cast:float(10UL ^^ cast:long(prec)));
    let size = 0UL, nb = f;	
    while (nb > 0UL) { nb /= 10UL; size ++; }
    if (size < prec) assert (false, "PrÃ©cision trop grande " + to!string (prec));
    let aux = [char; size + 1UL];
    for (id in 0UL .. size + 1UL) {
	if (id == prec) aux [aux.length - id - 1UL] = '.';
	else {
	    aux [aux.length - id - 1UL] =  cast:char (f % 10UL) + '0';
	    f /= 10UL;
	    }	    
    }
    return cast:string (aux);
}




