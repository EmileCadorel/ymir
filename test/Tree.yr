
def println (s : string) {
    let i = 0;
    while (i < s.length) {
	system (print_c, s[i]);
	i++;
    }
    system (print_c, '\n');
}

def print (c : char) {
    system (print_c, c);
}

def print (i : int) : void {
    if (i < 0) {
	system (print_c, '-');
	print (- i);
    } else {
	if (i > 9) print (i / 10);
	system (print_c, (i%10) + '0');
    }
}

def println (r : Tree, nb : int) : void {
    let i = 0;
    while (i < nb) {
	print (' ');
	i ++;
    }
    print (r.key);
    print (':');
    println (r.value);
    if (r.left !is null) println (r.left, nb + 4);
    if (r.right !is null) println (r.right, nb + 4);
}

def addNode (root : Tree, node : Tree) : void {
    if (node.key < root.key) {
	if (root.left !is null) addNode (root.left, node);
	else root.left = node;	
    } else {
	if (root.right !is null) addNode (root.right, node);
	else root.right = node;
    }
}

struct (left : Tree, right : Tree, key : int, value : string) Tree;

def main () {
    let root = Tree (null, null, 0, "Root");
    addNode (root, Tree (null, null, -1, "Left"));
    addNode (root, Tree (null, null, -3, "Left2"));
    addNode (root, Tree (null, null, 1, "Right"));
    addNode (root, Tree (null, null, 2, "Right2"));
    println (root, 0);
}


